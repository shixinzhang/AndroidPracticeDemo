apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    defaultConfig {
        applicationId "net.sxkeji.androiddevartiestpritice"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.1"
        manifestPlaceholders = [
                UMENG_CHANNEL_VALUE: "default"
        ]
        //使用VectorDrawableCompat
//        vectorDrawables.useSupportLibrary(true)
    }

    signingConfigs {
        debugKey {
//            keyAlias 'debugAlias'
            //不能变
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file('debug.keystore')
            storePassword 'android'
        }
        releaseKey {
            //自己生成一个
            keyAlias 'shixinzhangAlias'
            keyPassword 'shixinzhang'
            storeFile file('zsxTestKeyStore.jks')
            storePassword 'shixinzhang'
        }
    }

    buildTypes {
        release {
            //版本后缀
            versionNameSuffix "_release"
            //Zip优化
            zipAlignEnabled true
            //删除无用资源
            shrinkResources true
            //签名配置文件
            signingConfig signingConfigs.releaseKey


            //混淆、清除
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            //关闭 Log 标签
            buildConfigField "boolean", "LOG_DEBUG", "false"

        }
        debug {
            versionNameSuffix "_debug"
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debugKey
            buildConfigField "boolean", "LOG_DEBUG", "true"

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        //执行lint检查，有任何的错误或者警告提示，都会终止构建，我们可以将其关掉。
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }

    productFlavors {
        huawei {}
        tencent {}
        wandoujia {}
    }

    productFlavors.all {
        flavor ->
            flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
//            buildConfigField 'String', 'API_SERVER_URL', 'http://www.baidu.com'
    }

    //使用渠道名打包apk
    android.applicationVariants.all {
        variant ->
            def flavorName = "_${variant.productFlavors[0].name}"

            variant.outputs.each {
                output ->
                    flavorName += variant.buildType.versionNameSuffix
                    def fileName = "testAPK_V" + android.defaultConfig.versionName + flavorName
//                    fileName += variant.buildType.minifyEnable ? "_proguard" :
//                            !variant.buildType.zipAlignEnabled ? "_unalign" : ""
                    fileName += ".apk"

                    //生成apk名称为: testAPK_V1.0_huawei_release_proguard.apk
                    output.outputFile = new File(output.outputFile.parent, fileName)
            }
    }
}

final APPCOMPAT_VERSION = '23.4.0'
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile "com.android.support:appcompat-v7:$APPCOMPAT_VERSION"
    compile "com.android.support:recyclerview-v7:$APPCOMPAT_VERSION"
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.0'
    compile files('libs/jword-1.0.jar')
    compile 'org.greenrobot:eventbus:3.0.0'
    compile "com.facebook.react:react-native:+"
}
